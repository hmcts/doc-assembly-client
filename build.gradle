plugins {
  id 'maven-publish'
  id 'java-library'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.3'
  id 'com.github.ben-manes.versions' version '0.52.0'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
version buildNumber

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.25.0'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

def gitRepo = "https://github.com/hmcts/doc-assembly-client"

def pomConfig = {
  licenses {
    license {
      name "MIT License"
      url "http://www.opensource.org/licenses/mit-license.php"
      distribution "repo"
    }
  }

  scm {
    url "https://github.com/hmcts/doc-assembly-client"
  }
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {

  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }

  publications {
    Main(MavenPublication) {

      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'doc-assembly-client'
      version project.version

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Client library for communicating with doc assembly api')
        root.appendNode('name', 'Doc assembly client')
        root.appendNode('url', 'https://github.com/hmcts/doc-assembly-client')
        root.children().last() + pomConfig
      }
    }
  }
}

ext {
  springCloudVersion = '2025.0.0'
  junitJupiterVersion = '5.13.2'
  lombokVersion = '1.18.38'
  mockitoVersion = '5.18.0'
}

dependencies {
  api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  api group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  api group: 'io.github.openfeign', name: 'feign-jackson'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.13.2'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
  dependencies {
    dependencySet(group: 'org.junit.platform', version: '1.13.1') {
        entry 'junit-platform-commons'
        entry 'junit-platform-engine'
    }
  }
}

bootJar {
  archiveClassifier = 'boot'
  enabled = false
}

jar {
  archiveClassifier = ''
  enabled = true
}

test {
  useJUnitPlatform()
}
